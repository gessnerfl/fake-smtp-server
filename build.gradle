plugins {
    id "pl.allegro.tech.build.axion-release" version "1.14.4"
    id "org.springframework.boot" version "3.0.4"
    id "org.sonarqube" version "4.0.0.2929"
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id "com.github.node-gradle.node" version "3.5.1"
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "io.spring.dependency-management"
apply plugin: "jacoco"
apply plugin: "org.sonarqube"

scmVersion {
    tag {
        prefix = ""
    }
}

group = "de.gessnerfl"
description = """Fake SMTP Server :: Simple SMTP Server which stores emails in memory and lists email in a web ui"""
project.version = project.findProperty("projectVersion") ?: scmVersion.version
project.archivesBaseName = "fake-smtp-server"

ext.bootstrapVersion = "5.2.3"
ext.scmUrl = "git@github.com:gessnerfl/fake-smtp-server.git"
ext.projectUrl = "https://github.com/gessnerfl/fake-smtp-server"

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-mail")
    implementation("commons-io:commons-io:2.11.0")
    implementation('org.flywaydb:flyway-core')
    implementation("org.glassfish.jaxb:jaxb-runtime:4.0.2")
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.3')

    runtimeOnly("com.h2database:h2:2.1.214")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.ow2.asm:asm:9.4")
    testImplementation("org.apache.commons:commons-lang3:3.12.0")
    testImplementation("org.hamcrest:hamcrest-core:2.2")
}

java {
    withSourcesJar()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:unchecked"
    }
}

node {
    nodeProjectDir = file("${project.projectDir}/webapp")
}

tasks.register('webappNpmBuild', NpmTask) {
    dependsOn(npmInstall)
    args = ["run", "build"]
}

tasks.register('webappNpmTest', NpmTask) {
    dependsOn(webappNpmBuild)
    args = ["run", "test"]
}

bootJar {
    dependsOn(compileJava, webappNpmBuild)
    from("webapp/build") {
        into("BOOT-INF/classes/static/")
    }
}

springBoot {
    buildInfo {
        properties {}
    }
}

sonar {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gessnerfl-github"
        property "sonar.projectName", "Fake SMTP Server"
        property "sonar.projectKey", "de.gessnerfl.fake-smtp-server"
        property "sonar.sources", ${sourceSets.main.allJava.srcDirs} + ",webapp/src"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

jib {
    from {
        image = 'amazoncorretto:11.0.17-al2'
        container {
            ports = ["5080", "5081", "5025"]
            creationTime = 'USE_CURRENT_TIMESTAMP'
            jvmFlags = ['-Djava.security.egd=file:/dev/./urandom', '-Dfile.encoding=utf-8']
        }
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = 'gessnerfl/fake-smtp-server'
        tags = [scmVersion.version, 'latest']
        auth {
            username = DOCKERHUB_USERNAME
            password = DOCKERHUB_TOKEN
        }
    }
}
